// Copyright 2020 Visual Design Cafe. All rights reserved.
// Package: Nature Shaders
// Website: https://www.visualdesigncafe.com/nature-shaders
// Documentation: https://support.visualdesigncafe.com/hc/categories/900000043503

#ifndef NODE_HSL_INCLUDED
#define NODE_HSL_INCLUDED

#include "../Common.cginc"

float3 _RgbToHsv( float3 In )
{
    // RGB to HSV
    float4 K = float4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    float4 P = lerp(float4(In.bg, K.wz), float4(In.gb, K.xy), step(In.b, In.g));
    float4 Q = lerp(float4(P.xyw, In.r), float4(In.r, P.yzx), step(P.x, In.r));
    float D = Q.x - min(Q.w, Q.y);
    float E = 1e-4;
    return float3(abs(Q.z + (Q.w - Q.y)/(6.0 * D + E)), D / (Q.x + E), Q.x);
}

float3 _HsvToRgb( float3 hsv )
{
    // HSV to RGB
    float4 K2 = float4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    float3 P2 = abs(frac(hsv.xxx + K2.xyz) * 6.0 - K2.www);
    return hsv.z * lerp(K2.xxx, saturate(P2 - K2.xxx), hsv.y);
}

float3 _Saturation(float3 In, float Saturation)
{
    float luma = dot(In, float3(0.2126729, 0.7151522, 0.0721750));
    return  luma.xxx + Saturation.xxx * (In - luma.xxx);
}

void HSL_float( float4 color, float3 hsl, out float4 colorOut )
{
    float3 hsv = _RgbToHsv( color.rgb );
    hsv.x = frac(hsv.x + hsl.x);
    hsv.z += hsl.z;
    colorOut = float4(_HsvToRgb( hsv ), color.a);
    colorOut = float4(_Saturation( colorOut.rgb, (hsl.y + 1) ), color.a);
}

#endif